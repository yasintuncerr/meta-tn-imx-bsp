#@TYPE: Machine
#@NAME: NXP i.MX 95 19x19 Evaluation Kit with LPDDR5
#@SOC: i.MX95
#@DESCRIPTION: Machine configuration for TechNexion EDM-IMX95
#@MAINTAINER: Richard Hu <richard.hu@technexion.com>

MACHINEOVERRIDES =. "mx95:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc
require conf/machine/tn-base.inc

KERNEL_DEVICETREE = " \
    freescale/imx95-edm-evm.dtb \
    freescale/imx95-edm-evm-lvds-vl10112880.dtb \
    freescale/imx95-edm-evm-lvds-vl156192108.dtb \
    freescale/imx95-edm-evm-mipi2hdmi-adv7535.dtb \
    freescale/imx95-edm-evm-tevs-csi1.dtb \
    freescale/imx95-edm-evm-tevs.dtb \
    freescale/imx95-edm-evm-fusion-lvds-vl10112880.dtb \
    freescale/imx95-edm-evm-fusion-lvds-vl156192108.dtb \
    freescale/imx95-edm-evm-vls-gm2-csi0-lvds-vl10112880.dtb \
    freescale/imx95-edm-evm-vls-gm2-csi1-lvds-vl10112880.dtb \
    freescale/imx95-edm-evm-vls-gm2-ext-fsync-lvds-vl10112880.dtb \
    freescale/imx95-edm-evm-vls-gm2-fsync-lvds-vl10112880.dtb \
    freescale/imx95-edm-evm-vls-gm2-lvds-vl10112880.dtb \
"

# U-boot specifics
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "edm-imx95_defconfig,sdcard"

ATF_PLATFORM = "imx95"
OEI_CONFIGS = "ddr tcm"
OEI_CORE   = "m33"
OEI_SOC    = "mx95"
OEI_BOARD  = "edm-imx95"
DDR_TYPE   = "lpddr5"

IMXBOOT_TARGETS_BASENAME = "flash"

IMXBOOT_VARIANTS = "alt jailhouse netc rpmsg sof"

# Multiple system manager configs by IMXBOOT_VARIANT
SYSTEM_MANAGER_CONFIG = \
    "${@bb.utils.contains('IMXBOOT_VARIANT', 'alt',       'mx95alt', \
        bb.utils.contains('IMXBOOT_VARIANT', 'jailhouse', 'mx95evkjailhouse', \
        bb.utils.contains('IMXBOOT_VARIANT', 'netc',      'mx95netc', \
        bb.utils.contains('IMXBOOT_VARIANT', 'rpmsg',     'mx95evkrpmsg', \
        bb.utils.contains('IMXBOOT_VARIANT', 'sof',       'mx95evksof', \
                                                          'mx95evk', d), d), d), d), d)}"

# imx-boot (flash.bin) targets based on UBOOT_CONFIG and IMXBOOT_VARIANT
IMXBOOT_TARGETS_SD = " \
    ${@bb.utils.contains('IMXBOOT_VARIANT', 'alt',       '${IMXBOOT_TARGETS_BASENAME}_alt', \
       bb.utils.contains('IMXBOOT_VARIANT', 'jailhouse', '${IMXBOOT_TARGETS_BASENAME}_jailhouse', \
       bb.utils.contains('IMXBOOT_VARIANT', 'netc',      '${IMXBOOT_TARGETS_BASENAME}_netc', \
       bb.utils.contains('IMXBOOT_VARIANT', 'rpmsg',     '${IMXBOOT_TARGETS_BASENAME}_lpboot_sm_a55', \
       bb.utils.contains('IMXBOOT_VARIANT', 'sof',       '${IMXBOOT_TARGETS_BASENAME}_a55', \
                                                         '${IMXBOOT_TARGETS_BASENAME}_all ${IMXBOOT_TARGETS_BASENAME}_a55', d), d), d), d), d)} \
"

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

DDR_FIRMWARE_NAME = " \
    lpddr5_dmem_v202409.bin \
    lpddr5_dmem_qb_v202409.bin \
    lpddr5_imem_v202409.bin \
    lpddr5_imem_qb_v202409.bin \
"
M4_DEFAULT_IMAGE_MX95 = "${@bb.utils.contains('SYSTEM_MANAGER_CONFIG', 'mx95netc', 'imx95-19x19-evk_m7_TCM_netc_share.bin', \
                                                                                   'imx95-19x19-evk_m7_TCM_power_mode_switch.bin', d)}"

IMX_BOOT_SOC_TARGET = "iMX95"
IMX_BOOT_SEEK = "32"

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

IMX_DEFAULT_BSP = "nxp"
